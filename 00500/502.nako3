### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?502
### タイトル=迷路自動生成(穴掘り法)
### 作者=クジラ飛行机(user_id=1)
### ライセンス=CC0
### 説明=穴掘り法で迷路を自動生成します。
### 対象バージョン=3.2.4
### URL=
### 種類=wnako
### タグ=
### 更新日時=2021-04-30 21:39:20
###

MWを8に定める。
55の迷路作成して迷路表示。

●(列数の)迷路作成とは
　　迷路は[]
　　Yを0から(列数-1)まで繰り返す
　　　　迷路[Y] = []
　　　　Xを0から(列数-1)まで繰り返す
　　　　　　迷路[Y][X] = 1
　　　　ここまで。
　　ここまで。
　　# 左上を起点として穴掘りをはじめる
　　迷路で1と1の迷路掘削処理。
ここまで

●(迷路でXとYの)迷路掘削処理とは
　　UDLR＝[[0,-1],[0,1],[-1,0],[1,0]]
　　列数＝迷路の要素数
　　UDLRの配列シャッフル。
　　UDLRを反復
　　　　RX=対象[0]。RY=対象[1]
　　　　# 2マス先を調べる
　　　　X2 = RX * 2 + X
　　　　Y2 = RY * 2 + Y
　　　　# 迷路の外なら中止
　　　　もし、(X2<=0)または(X2>=列数-1)ならば続ける。
　　　　もし、(Y2<=0)または(Y2>=列数-1)ならば続ける。
　　　　もし、迷路[Y2][X2]=0ならば続ける。
　　　　# 穴を掘る
　　　　迷路[Y+RY][X+RX] = 0
　　　　迷路[Y2][X2] = 0
　　　　# 再帰的に掘削する
　　　　迷路でX2とY2の迷路掘削処理を迷路に代入。
　　ここまで。
　　それは迷路。
ここまで

●(迷路を)迷路表示とは
　　列数＝迷路の要素数。
　　Yを0から(列数-1)まで繰り返す
　　　　Xを0から(列数-1)まで繰り返す
　　　　　　V = 迷路[Y][X]
　　　　　　XX=X×MW
　　　　　　YY=Y×MW
　　　　　　灰色に線色設定。
　　　　　　もしV＝0ならば、白色に塗り色設定。
　　　　　　違えば、茶色に塗り色設定。
　　　　　　[XX, YY, MW, MW]に四角描画。
　　　　ここまで。
　　ここまで。
ここまで。