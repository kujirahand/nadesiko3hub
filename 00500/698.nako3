### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?698
### タイトル=ch5/sinkeisuijaku_graphic.nako3
### 作者=クジラ飛行机(user_id=1)
### ライセンス=MIT
### 説明=「なでしこ3本」5章のサンプル「sinkeisuijaku_graphic.nako3」です。
### 対象バージョン=3.2.23
### URL=https://nadesi.com/top/go.php?46
### 種類=wnako
### タグ=なでしこ3本
### 更新日時=2021-07-07 00:50:06
###

### なでしこ3本 5章 のサンプル ###
# [file] ../src/ch5/sinkeisuijaku_graphic.nako3
# [説明URL] https://nadesi.com/top/go.php?46

# 初期設定 --- (*1)
画像URL=「https://n3s.nadesi.com/image.php?f=25.png」
W＝80 # カード画像1枚の幅
カード一覧＝[1,1,2,2,3,3,4,4]
カード一覧を配列シャッフル。
選択＝0。A=0。B=0。# 選んだカードを覚えておく変数
進行＝0。# ゲームの進行を管理する変数
ゲーム進行。
# マウスを押した時のイベントを設定 --- (*2)
描画中キャンバスをマウス押した時には
　　選択＝INT(マウスX÷W）＋1
　　ゲーム進行。
ここまで。

●ゲーム進行とは # --- (*3)
　　もし、進行が0ならば # --- (*4)
　　　　「1つ目の箱を選んでください！」と情報表示。
　　　　A=0。B=0。進行＝1。
　　　　0と0のカード描画。
　　違えば、もし、進行が1ならば # --- (*5)
　　　　「2つ目の箱を選んでください！」と情報表示。
　　　　A=選択。進行＝2。
　　　　Aと0のカード描画。
　　違えば、もし、進行が2ならば # --- (*6)
　　　　もし、A=選択ならば、戻る。
　　　　B=選択。
　　　　AとBのカード描画。
　　　　進行＝3
　　　　カード正誤判定。
　　違えば、もし、進行が3ならば # --- (*7)
　　　　進行＝0
　　　　ゲーム進行。
　　ここまで。
ここまで。

●カード正誤判定とは # --- (*8)
　　もし(カード一覧[A-1]=カード一覧[B-1])ならば
　　　　「当たり！（ここをクリック）」と情報表示。
　　　　カード一覧[A-1]=0
　　　　カード一覧[B-1]=0
　　　　もし((カード一覧の配列合計)=0)ならば # --- (*9)
　　　　　　「ゲームクリア」と情報表示。
　　　　　　カード一覧＝[1,1,2,2,3,3,4,4]
　　　　　　カード一覧を配列シャッフル。
　　　　ここまで。
　　違えば
　　　　「ハズレ（ここをクリック）」と情報表示。
　　ここまで。
ここまで。

●(Sと)情報表示とは # --- (*10)
　　[0,80,640,80]の描画クリア。
　　20に描画フォント設定。
　　[10,110]にSを文字描画。
ここまで。

●(AとBの)カード描画とは # --- (*11)
　　Nを0から7まで繰り返す
　　　　C=5 # 箱の絵
　　　　もし(N=(A-1))または(N=(B-1))ならば
　　　　　　C=カード一覧[N] # カードの絵柄
　　　　違えば、もし、カード一覧[N]=0ならば
　　　　　　C=0 # クリア画像
　　　　ここまで。
　　　　# 指定画像の描画
　　　　画像領域＝[C×W,0,W,W]
　　　　描画領域＝[N×W,0,W,W]
　　　　画像URLの画像領域を描画領域へ画像部分描画。
　　ここまで。
ここまで。