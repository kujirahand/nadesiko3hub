### [作品情報]
### タイトル=アナログ時計(マイナビ連載より)
### 作者=クジラ飛行机(user_id=1)
### ライセンス=CC0
### 説明=カメを使って手軽にアナログ時計を描画してみました。
### 対象バージョン=3.2.7
### URL=https://news.mynavi.jp/article/nadeshiko-26/
### 種類=wnako
### タグ=
### 更新日時=2021-05-06 15:19:11
###

# マイナビ連載26回目 - アナログ時計
# https://news.mynavi.jp/article/nadeshiko-26/

# 時計のサイズを指定 
 CW=300 # 幅
 CH=300 # 高さ
 CX=CW÷2 # 中心点
 CY=CH÷2
 LT=5 # 左上マージン

 カメ作成。
 0にカメ速度設定。
 「時計描画」を1秒毎。# --- (*1)

 ●時計描画
     盤描画。
     # 現在時刻を得る --- (*2)
     今を「:」で区切る。
     時=それ[0]
     分=それ[1]
     秒=それ[2]
     # 角度計算 --- (*3)
     時角度＝(時%12)÷12×360
     分角度＝分÷60×360
     秒角度＝秒÷60×360
     # 針を描画 --- (*4)
     黒色の12を(CX×0.5)と時角度で針描画。# 時
     青色の8を(CX×0.8)と分角度で針描画。# 分
     赤色の4をCXと秒角度で針描画。# 秒
 ここまで。

 ●（色のサイズを長さと角度で）針描画
     [LT+CX,LT+CY]へカメ起点移動。
     色にカメペン色設定。
     サイズにカメペンサイズ設定。
     角度にカメ角度設定。
     長さだけカメ進む。
     90だけカメ右回転。
 ここまで。

 ●盤描画
     5に線太設定。
     水色に塗色設定。
     黒色に線色設定。
     [LT+CX,LT+CY]へCXの円描画。
 ここまで。