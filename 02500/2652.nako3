### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?2652
### タイトル=TRndM5 - 自動フレーズメーカー
### 作者=クジラ飛行机(user_id=0)
### ライセンス=MIT
### 説明=MML演奏を使ったフレーズ自動作成ツールです。
### 対象バージョン=3.4.22
### URL=
### 種類=wnako
### タグ=MML,mml-tools
### 更新日時=2023-10-28 20:57:13
###

# TRndM5 - 自動フレーズメーカー
#-----------------------------------------------
!『https://cdn.jsdelivr.net/npm/nadesiko3-music@1.0.2/nadesiko3-music.js』を取り込む
オンにMIDIループ再生設定。
#-----------------------------------------------
『<h3 style="padding: 0.3em; background-color: orange;">
♫ TRndM5</h3>』のラベル作成。
#-----------------------------------------------
FF=空の『
BPM=120
NOTE=c,d,e,f,g,a,b,`c,`d,`e,r,^
音階=3
個数=32
音符=16
』をフォーム作成。
#-----------------------------------------------
生成ボタン＝「Generate & Play」のボタン作成。
再生ボタン＝「Play」のボタン作成。
停止ボタン＝「Stop」のボタン作成。
停止ボタンをクリックした時には
　　MML停止。
ここまで。
改行作成
「<hr><b>MML:</b><br>」のラベル作成。
MML結果=「」のエディタ作成。
MML結果へ{"width":"40em"}をDOMスタイル一括設定。
「<br><br><b>伴奏を以下に指定:</b><br>」のラベル作成。
伴奏エリア=『TR(10)CH(10)
[2
  [4 l8 R{ bhsh : bhsh }] R{bsss}
]
』のテキストエリア作成。
伴奏エリアへ{"height":"5em"}をDOMスタイル一括設定。
「BODY」へ{}をDOMスタイル一括設定。
#-----------------------------------------------
生成ボタンをクリックした時には
　　FFのフォーム入力一括取得してFに代入。
　　BPM = F["BPM"]
　　NOTE = F["NOTE"]
　　OCTAVE = F["音階"]
　　LEN=F["音符"]
　　SIZE=F["個数"]
　　SIZE_D=INT(SIZE÷8)
　　NOTES=NOTEを「,」で区切る
　　NOTES_L=NOTESの要素数
　　MML=「o{OCTAVE}l{LEN}; [4 」
　　Iを0から(SIZE-1)まで繰り返す
　　　　もし、(I>0)かつ(I%SIZE_D=0)ならば、MML=MML&「 | 」
　　　　R=NOTES_Lの乱数
　　　　N=NOTES[R]
　　　　もし(I=0)かつ(N=「^」)ならば、N=「r」
　　　　MML=MML&N
　　ここまで
　　MML=MML&「 ] 」
　　MML結果にMMLをテキスト設定。
　　再生処理。
ここまで。
#-----------------------------------------------
再生ボタンをクリックした時には
　　再生処理。
ここまで。

●再生処理とは
　　MML停止。
　　MML=MML結果からテキスト取得。
　　FFのフォーム入力一括取得してFに代入。
　　BPM = F["BPM"]
　　MML_A=「TEMPO={BPM}; TR(1)」
　　伴奏＝伴奏エリアからテキスト取得
　　MML_A=MML&「;;;{伴奏}」
　　MML_AをMML演奏。
ここまで。