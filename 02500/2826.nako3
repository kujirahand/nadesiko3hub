### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?2826
### タイトル=スゴロクその1（共通テスト2021年情報関係基礎）
### 作者=Hanako.S(user_id=0)
### ライセンス=CC0
### 説明=2021年実施の共通テストの「情報関係基礎」のプログラム問題のプログラムを実際につくってみました。その1はオバケが登場しないシンプルバージョンです。
### 対象バージョン=3.6.7
### URL=
### 種類=wnako
### タグ=共通テスト
### 更新日時=2024-06-18 13:35:46
###

全描画クリア。
0に線太設定。「#000077」に線色設定。「#000077」に塗色設定。
[0,0,800,600]へ四角描画。
マス種類=[「Ｓ」,「　」，「崖」,「穴」,「沼」,「Ｇ」]
マス係数=[1,1,-1,0,0.5,1]
地図=[0,0,1,2,4,1,3,1,3,1,1,2,4,1,2,5]
Masu=[]
iを1から15まで繰り返す
　　Masu@i=マス係数@地図@i
ここまで。
Koma=[[],[],[]]
Saikoro=[[],[],[]]
ninzu=2
owari=0
r=0。i=0。
結果=「」。
iを1からninzuまで繰り返す
　　Koma@i,0=1
ここまで。

「32px sans-serif」に描画フォント設定。
0に線太設定。灰色に線色設定。白色に塗色設定。
[20,48]へ「スゴロク（共通テスト2021年）」を文字描画。
iを1から15まで繰り返す
　　x=-25+50*i。y=100。z=地図@i。
　　x,yへzのマス描画。
　　「16px sans-serif」に描画フォント設定。
　　0に線太設定。灰色に線色設定。白色に塗色設定。
　　マス番号=「 {i}」で2文字右部分。
　　[x+13,y-6]へマス番号を文字描画。
ここまで。
0のコマ表示。
テーブル表示。
2秒待つ。

(owari=0)の間
　　r=r+1
　　iを１からninzuまで繰り返す
　　　　Saikoro@i,r=（6の乱数）+1
　　　　k=Koma@i,(r-1)
　　　　bairitu=Masu@k
　　　　もし、（bairitu=0）かつ（Saikoro@i,r>=4）ならば、
　　　　　　bairitu=1
　　　　ここまで。
　　　　idou=（Saikoro@i,r*bairitu）の整数部分
　　　　Koma@i,r=k+idou
　　　　もし、Koma@i,r<1ならば、Koma@i,r=1
　　　　もし、Koma@i,r>=15ならば、
　　　　　　Koma@i,r=15
　　　　　　owari=1
　　　　ここまで。
　　ここまで。
　　コマ消去。
　　rのコマ表示。
　　rのラウンド表示。
　　2秒待つ。
ここまで。
もし、Koma@1,r>Koma@2,rならば、結果=「Ｎの勝ち！」
もし、Koma@1,r<Koma@2,rならば、結果=「Ｓの勝ち！」
もし、Koma@1,r=Koma@2,rならば、結果=「引き分け！」
「24px sans-serif」に描画フォント設定。
0に線太設定。灰色に線色設定。白色に塗色設定。
[660,255]へ結果を文字描画。


●テーブル表示とは
　　「#aaaaaa」に線色設定。
　　iを0から5まで繰り返す
　　　　[20,300+50*i]から[20+760,300+50*i]まで線描画。
　　ここまで。
　　iを0から19まで繰り返す
　　　　[20+40*i,300]から[20+40*i,550]まで線描画。
　　ここまで。
　　0に線太設定。「#aaaaaa」に線色設定。「#000077」に塗色設定。
　　[20,300,120,50]へ四角描画。
　　iを0から1まで繰り返す
　　　　[20,350+100*i,40,100]へ四角描画。
　　ここまで。
　　iを0から3まで繰り返す
　　　　[60,350+50*i,80,50]へ四角描画。
　　ここまで。
　　0に線太設定。白色に線色設定。白色に塗色設定。
　　「24px sans-serif」に描画フォント設定。
　　[30,335]へ「ラウンド」を文字描画。
　　iを0から15まで繰り返す
　　　　Ｒ番号=「 {i}」で2文字右部分。
　　　　[144+40*i,335]へＲ番号を文字描画。
　　ここまで。
　　iを0から1まで繰り返す
　　　　[75,385+100*i]へ「出目」を文字描画。
　　　　[75,435+100*i]へ「位置」を文字描画。
　　ここまで。
　　[28,410]へ「Ｎ」を文字描画。
　　[28,510]へ「Ｓ」を文字描画。
　　iを0から1まで繰り返す
　　　　[149,385+100*i]へ「－」を文字描画。
　　　　[144,435+100*i]へ「 1」を文字描画。
　　ここまで。
ここまで。

●（rの）ラウンド表示とは
　　0に線太設定。白色に線色設定。白色に塗色設定。
　　「24px sans-serif」に描画フォント設定。
　　iを1から2まで繰り返す
　　　　[152+40*r,285+100*i]へSaikoro@i,rを文字描画。
　　　　マス番号=「 {Koma@i,r}」で2文字右部分。
　　　　[144+40*r,335+100*i]へマス番号を文字描画。
　　ここまで。
ここまで。

●（rの）コマ表示とは
　　0に線太設定。白色に線色設定。白色に塗色設定。
　　「24px sans-serif」に描画フォント設定。
　　kx=50*(Koma@1,r)-15。ky=180。
　　[kx,ky]へ「Ｎ」を文字描画。
　　kx=50*(Koma@2,r)-15。ky=210。
　　[kx,ky]へ「Ｓ」を文字描画。
ここまで。

●コマ消去とは
　　0に線太設定。「#000077」に線色設定。「#000077」に塗色設定。
　　[21,158,750,58]へ四角描画。
ここまで。
●（x,yへzの）マス描画とは
　　2に線太設定。「#aaaaaa」に線色設定。白色に塗色設定。
　　もし、z=2ならば、「#cc9900」に塗色設定。
　　もし、z=3ならば、「#cccccc」に塗色設定。
　　もし、z=4ならば、「#ccdd99」に塗色設定。
　　[x,y,46,46]へ四角描画。
　　0に線太設定。灰色に線色設定。黒色に塗色設定。
　　「36px sans-serif」に描画フォント設定。
　　[x+5,y+37]へマス種類@zを文字描画。
ここまで。