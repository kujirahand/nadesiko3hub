### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?2521
### タイトル=プログラムでシダを描画するver2
### 作者=クジラ飛行机(user_id=0)
### ライセンス=CC0
### 説明=過去に投稿したシダを描画するを少し改良してみました。
### 対象バージョン=3.4.20
### URL=https://n3s.nadesi.com/index.php?action=edit&page=370
### 種類=wnako
### タグ=
### 更新日時=2023-07-04 17:18:02
###

# -------------------------------------------------
# プログラムでシダを描画するver.2
# https://n3s.nadesi.com/id.php?370 を改造したもの
# -------------------------------------------------
N = 18
xm = 0
ym = 0.5
h = 0.8
width = 340
height = 500
TM = 0 # タイマー管理用

W1x = 関数(x, y) それは、0.836 * x + 0.044 * y。ここまで。
W1y = 関数(x, y) それは、-0.044 * x + 0.836 * y + 0.169。ここまで。
W2x = 関数(x, y) それは、-0.141 * x + 0.302 * y。ここまで。
W2y = 関数(x, y) それは、0.302 * x + 0.141 * y + 0.127。ここまで。
W3x = 関数(x, y) それは、0.141 * x - 0.302 * y。ここまで。
W3y = 関数(x, y) それは、0.302 * x + 0.141 * y + 0.169。ここまで。
W4x = 関数(x, y) それは、0。ここまで。
W4y = 関数(x, y) それは、0.175337 * y。ここまで。

●(kのxとyで)fとは
　　TM=TM+1
　　もし、TM%1000=0ならば、0.001秒待つ。
　　もし、0 < kならば
　　　　f(k - 1, W1x(x, y), W1y(x, y))
　　　　もし、(10の乱数) < 3ならば
　　　　　　f(k - 1, W2x(x, y), W2y(x, y))
　　　　ここまで。
　　　　もし、(10の乱数) < 3ならば
　　　　　　f(k - 1, W3x(x, y), W3y(x, y))
　　　　ここまで。
　　　　もし、(10の乱数) < 3ならば
　　　　　　f(k - 1, W4x(x, y), W4y(x, y))
　　　　ここまで。
　　違えば
　　　　s = 490
　　ここまで。
　　XX = INT(x * s + width * 0.5)
　　YY = INT(height - y * s)
　　[XX, YY, 1, 1]へ四角描画。
ここまで。

C=RGB(0,129,0)
1に線太設定。Cに線色設定。Cに塗色設定。
f(N, 0, 0)
「14px sans-serif」に描画フォント設定。
MSG=「44K344OA44Gn44GZ44CC44GM44KT44Gw44KK44G+44K344OA44CC44K344OA44Gn44GZ44CC」をBASE64デコード。
[30,530]へMSGを文字描画。