### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?2831
### タイトル=スゴロクその2（共通テスト2021年情報関係基礎）
### 作者=Hanako.S(user_id=0)
### ライセンス=CC0
### 説明=2021年実施の共通テストの「情報関係基礎」のプログラム問題のプログラムを実際につくってみました。その2はオバケが登場するバージョンです。
### 対象バージョン=3.6.7
### URL=
### 種類=wnako
### タグ=共通テスト
### 更新日時=2024-06-29 13:56:30
###

全描画クリア。
0に線太設定。「#000077」に線色設定。「#000077」に塗色設定。
[0,0,800,600]へ四角描画。
マス種類=[「Ｓ」,「　」，「崖」,「穴」,「沼」,「Ｇ」]
マス係数=[1,1,-1,0,0.5,1]
地図=[0,0,1,4,2,1,1,1,1,1,1,4,1,2,3,5]
Masu=[]
iを1から15まで繰り返す
　　Masu@i=マス係数@地図@i
ここまで。
Koma=[[],[],[]]
Saikoro=[[],[],[]]
ninzu=2
owari=0
r=0。
obake=6。a=0。
i=0。
結果=「」。
iを1からninzuまで繰り返す
　　Koma@i,0=1
ここまで。

「32px sans-serif」に描画フォント設定。
0に線太設定。灰色に線色設定。白色に塗色設定。
[20,48]へ「スゴロク（共通テスト2021年）」を文字描画。
「24px sans-serif」に描画フォント設定。
[480,48]へ「オバケありバージョン」を文字描画。
iを1から15まで繰り返す
　　x=-25+50*i。y=100。z=地図@i。
　　x,yへzのマス描画。
　　「16px sans-serif」に描画フォント設定。
　　0に線太設定。灰色に線色設定。白色に塗色設定。
　　マス番号=「 {i}」で2文字右部分。
　　[x+13,y-6]へマス番号を文字描画。
ここまで。
0のコマ表示。
テーブル表示。
2秒待つ。

(owari=0)の間
　　r=r+1
　　a=(r-1)%4
　　もし、a<2ならば、
　　　　obake=obake+1
　　違えば、
　　　　obake=obake-1
　　ここまで。
　　iを１からninzuまで繰り返す
　　　　Saikoro@i,r=（6の乱数）+1
　　　　k=Koma@i,(r-1)
　　　　bairitu=Masu@k
　　　　もし、（bairitu=0）かつ（Saikoro@i,r>=4）ならば、
　　　　　　bairitu=1
　　　　ここまで。
　　　　idou=（Saikoro@i,r*bairitu）の整数部分
　　　　Koma@i,r=k+idou
　　　　もし、（Koma@i,(r-1)<obake）かつ（Koma@i,r>obake）ならば、
　　　　　　Koma@i,r=obake
　　　　ここまで。
　　　　もし、Koma@i,r<1ならば、Koma@i,r=1
　　　　もし、Koma@i,r>=15ならば、
　　　　　　Koma@i,r=15
　　　　　　owari=1
　　　　ここまで。
　　ここまで。
　　コマ消去。
　　rのコマ表示。
　　rのラウンド表示。
　　2秒待つ。
ここまで。
もし、Koma@1,r>Koma@2,rならば、結果=「Ｎの勝ち！」
もし、Koma@1,r<Koma@2,rならば、結果=「Ｓの勝ち！」
もし、Koma@1,r=Koma@2,rならば、結果=「引き分け！」
「24px sans-serif」に描画フォント設定。
0に線太設定。灰色に線色設定。白色に塗色設定。
[660,272]へ結果を文字描画。


●テーブル表示とは
　　「#aaaaaa」に線色設定。
　　iを0から6まで繰り返す
　　　　[20,290+50*i]から[20+760,290+50*i]まで線描画。
　　ここまで。
　　iを0から19まで繰り返す
　　　　[20+40*i,290]から[20+40*i,590]まで線描画。
　　ここまで。
　　0に線太設定。「#aaaaaa」に線色設定。「#000077」に塗色設定。
　　iを0から1まで繰り返す
　　　　[20,290+50*i,120,50]へ四角描画。
　　ここまで。
　　iを0から1まで繰り返す
　　　　[20,390+100*i,40,100]へ四角描画。
　　ここまで。
　　iを0から3まで繰り返す
　　　　[60,390+50*i,80,50]へ四角描画。
　　ここまで。
　　0に線太設定。白色に線色設定。白色に塗色設定。
　　「24px sans-serif」に描画フォント設定。
　　[30,325]へ「ラウンド」を文字描画。
　　[43,375]へ「オバケ」を文字描画。
　　iを0から15まで繰り返す
　　　　Ｒ番号=「 {i}」で2文字右部分。
　　　　[144+40*i,325]へＲ番号を文字描画。
　　ここまで。
　　iを0から1まで繰り返す
　　　　[75,425+100*i]へ「出目」を文字描画。
　　　　[75,475+100*i]へ「位置」を文字描画。
　　ここまで。
　　[28,450]へ「Ｎ」を文字描画。
　　[28,550]へ「Ｓ」を文字描画。
　　iを0から1まで繰り返す
　　　　[149,425+100*i]へ「－」を文字描画。
　　　　[144,475+100*i]へ「 1」を文字描画。
　　ここまで。
　　[144,375]へ「 6」を文字描画。
ここまで。

●（rの）ラウンド表示とは
　　0に線太設定。白色に線色設定。白色に塗色設定。
　　「24px sans-serif」に描画フォント設定。
　　iを1から2まで繰り返す
　　　　[152+40*r,325+100*i]へSaikoro@i,rを文字描画。
　　　　マス番号=「 {Koma@i,r}」で2文字右部分。
　　　　[144+40*r,375+100*i]へマス番号を文字描画。
　　ここまで。
　　マス番号=「 {obake}」で2文字右部分。
　　[144+40*r,375]へマス番号を文字描画。
ここまで。

●（rの）コマ表示とは
　　0に線太設定。白色に線色設定。白色に塗色設定。
　　「24px sans-serif」に描画フォント設定。
　　kx=50*(obake)-18。ky=183。
　　[kx,ky]へ「👻」を文字描画。
　　kx=50*(Koma@1,r)-15。ky=212。
　　[kx,ky]へ「Ｎ」を文字描画。
　　kx=50*(Koma@2,r)-15。ky=237。
　　[kx,ky]へ「Ｓ」を文字描画。
ここまで。

●コマ消去とは
　　0に線太設定。「#000077」に線色設定。「#000077」に塗色設定。
　　[21,158,750,88]へ四角描画。
ここまで。

●（x,yへzの）マス描画とは
　　2に線太設定。「#aaaaaa」に線色設定。白色に塗色設定。
　　もし、z=2ならば、「#cc9900」に塗色設定。
　　もし、z=3ならば、「#cccccc」に塗色設定。
　　もし、z=4ならば、「#ccdd99」に塗色設定。
　　[x,y,46,46]へ四角描画。
　　0に線太設定。灰色に線色設定。黒色に塗色設定。
　　「36px sans-serif」に描画フォント設定。
　　[x+5,y+37]へマス種類@zを文字描画。
ここまで。