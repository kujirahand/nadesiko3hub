### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?699
### タイトル=ch5/sinkeisuijaku_tazuneru.nako3
### 作者=クジラ飛行机(user_id=1)
### ライセンス=MIT
### 説明=「なでしこ3本」5章のサンプル「sinkeisuijaku_tazuneru.nako3」です。
### 対象バージョン=3.2.23
### URL=https://nadesi.com/top/go.php?46
### 種類=wnako
### タグ=なでしこ3本
### 更新日時=2023-07-04 16:11:38
###

### なでしこ3本 5章 のサンプル ###
# [file] ../src/ch5/sinkeisuijaku_tazuneru.nako3
# [説明URL] https://nadesi.com/top/go.php?46

# カード一覧を初期化 --- (*1)
カード一覧＝[1,1,2,2,3,3,4,4]
カード一覧を配列シャッフル。

# ゲーム開始 --- (*2)
永遠の間繰り返す
　　# 1枚目のカードを選んでもらう --- (*3)
　　「[1枚目]1−8の何番をめくる？{0と0でカード表示}」と尋ねてAに代入。
　　# 2枚目のカードを選んでもらう --- (*4)
　　「[2枚目]1-8の何番をめくる？{Aと0でカード表示}」と尋ねてBに代入。
　　もし、(A=空)かつ(B=空)ならば、抜ける。
　　もし、(A=B)または(A=空)または(B=空)ならば、続ける。# 入力間違い
　　# 正誤判定 --- (*5)
　　もし(カード一覧[A-1]＝カード一覧[B-1])ならば
　　　　「⭐正解！{AとBでカード表示}」と言う。
　　　　カード一覧[A-1]＝0
　　　　カード一覧[B-1]＝0
　　　　もし、（カード一覧の配列合計）＝0ならば # --- (*6)
　　　　　　「ゲームクリア！」と言う。抜ける。
　　　　ここまで。
　　違えば
　　　　「😭残念。{AとBでカード表示}」と言う。
　　ここまで。
ここまで。

●（AとBで）カード表示とは # --- (*7)
　　机=[]
　　Nを0から7まで繰り返す
　　　　C=「■」
　　　　もし((N=(A-1))または(N=(B-1)))ならば # 指定場所
　　　　　　C=「[{カード一覧[N]}]」
　　　　違えば、もし(カード一覧[N]=0)ならば # 既に当てた場所
　　　　　　C=「👌」
　　　　ここまで。
　　　　机にCを配列追加。
　　ここまで。
　　それは、改行&(机を「-」で配列結合)。
ここまで。
