### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?2991
### タイトル=画像で当てる神経衰弱
### 作者=クジラ飛行机(user_id=0)
### ライセンス=CC0
### 説明=画像で当てる神経衰弱です！
### 対象バージョン=3.7.3
### URL=
### 種類=wnako
### タグ=w_noname
### 更新日時=2025-03-14 00:22:31
###

# --------------------------------------------------
# 画像で当てる神経衰弱
# --------------------------------------------------
画像枚数=10
画像ID=548
画像URL=「https://n3s.nadesi.com/image.php?f=」
開始時間＝0
ボタン一覧=[]
選択ボタン=-1
有効ボタン=[]
カードデッキ=[]
出力エリア＝空
残りカード=0

開始ボタン＝「神経衰弱を開始する」のボタン作成。
開始ボタンをクリックした時には：
　　開始ボタン＄有効＝オフ。
　　ボタン一括作成。
　　ゲーム開始。

●ゲーム開始とは：
　　カード配布。
　　開始時間＝今
　　ボタン一覧を反復：
　　　　対象$有効=オン

●カード配布とは：
　　# カードの初期化処理 --- (*1)
　　カードデッキ=[]
　　Iを0から(画像枚数-1)まで繰り返す：
　　　　カードデッキにIを配列追加。# 1つ目
　　　　カードデッキにIを配列追加。# 2つ目
　　カードデッキを配列シャッフル。
　　残りカード=画像枚数*2

●ボタン一括作成とは：
　　# ボタンの一括作成 --- (*2)
　　もし、(ボタン一覧の要素数) > 0ならば、戻る。
　　改行作成。改行作成。
　　Iで0から(画像枚数＊2-1)まで繰り返す：
　　　　B=「{(I+1)を2でゼロ埋め}」のボタン作成。
　　　　B$ポケット=I
　　　　B$幅=4em; B$高さ=3em
　　　　Bをクリックした時には：
　　　　　　対象をカード選択処理。
　　　　ボタン一覧[I] = B
　　　　もし、(I % 5) = 4ならば、改行作成。
　　出力エリア＝「1枚目を選択してください」のラベル作成。

●(Bを)カード選択処理とは：
　　# カードを選択した時の処理 --- (*3)
　　NO=対象$ポケット
　　C=カードデッキ[NO]
　　対象$有効＝オフ
　　もし、選択ボタン<0ならば：
　　　　# 1枚目の選択
　　　　選択ボタン=NO
　　　　Cと「2枚目を選択してください」を画像表示処理。
　　違えば：
　　　　# 2枚目の選択
　　　　C2=カードデッキ[選択ボタン]
　　　　もし、C2＝Cならば：
　　　　　　結果＝「当たり⭐」
　　　　　　残りカード＝残りカード-2
　　　　　　もし、残りカード=0ならば：
　　　　　　　　SEC=開始時間と今の秒差
　　　　　　　　結果＝結果＆「{SEC}秒でゲームクリア！！！！」
　　　　　　　　開始ボタン＄有効＝オン
　　　　　　違えば：
　　　　　　　　結果＝結果＆「次のカードを選んでください。」
　　　　　　　　ボタン一覧[選択ボタン]にオフをDOM有効設定。
　　　　違えば：
　　　　　　結果＝「はずれ😿 1枚目のカードを選んでください。」
　　　　　　有効ボタン=[選択ボタン, NO]
　　　　　　1秒後には：
　　　　　　　　有効ボタンを反復：
　　　　　　　　　　ボタン一覧[対象]にオンをDOM有効設定。
　　　　選択ボタン=-1
　　　　Cと結果を画像表示処理。

●(CNOとMSGを)画像表示処理とは：
　　# 画像を表示する処理 --- (*4)
　　URL=「{画像URL}{画像ID+CNO}.png」
　　出力エリア$HTML=「<h3>{MSG}</h3>
　　<img src="{URL}" style="width:19em">」