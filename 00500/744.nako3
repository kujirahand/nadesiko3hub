### [作品情報]
### タイトル=七並べ（トランプゲーム）
### 作者=クジラ飛行机(user_id=1)
### ライセンス=MIT
### 説明=トランプの七並べを遊ぶことができます。
### 対象バージョン=3.3.61
### URL=
### 種類=wnako
### タグ=ゲーム
### 更新日時=2022-08-28 23:55:44
###

#====================================
# 七並べ(トランプゲーム)
#====================================
# 変数
山札=[]
参加者=[]
机=[]
パス回数=[]
誰ターン＝0
情報＝「」
#====================================
カード絵柄=["♣","♠","♥","♦"]
カード番号＝["A","2","3","4","5","6","7","8","9","10","J","Q","K"]
カード枚数＝13×4
ゲーム初期化。

#====================================
●ゲーム初期化とは
　　カードシャッフル。
　　誰ターン=-1
　　次ターン。
ここまで。

#====================================
●カードシャッフルとは
　　＃カードをシャッフル
　　山札=[]
　　Iを0から(カード枚数-1)まで繰り返す
　　　　山札[I] = I
　　ここまで。
　　山札を配列シャッフル。
　　＃参加者に割り振る
　　参加者=[[],[],[],[]]
　　Iを0から(カード枚数-1)まで繰り返す
　　　　人＝I％4
　　　　札＝山札[I]
　　　　番号＝札%13
　　　　もし、番号＝6ならば続ける。# [7]なら追加しない
　　　　参加者[人]に札を配列追加。
　　ここまで。
　　＃机を初期化
　　机=[]
　　Iを0から(カード枚数-1)まで繰り返す
　　　　机[I] = -1
　　　　番号＝I % 13
　　　　もし、番号＝6ならば、机[I]=I。
　　ここまで。
　　＃参加者0の手札をソート
　　参加者[0]を配列数値ソート。
　　パス回数＝[0,0,0,0]
ここまで。

#====================================
●机表示とは
　　＃机表示
　　机HTML＝「<table>」
　　絵柄を0から3まで繰り返す
　　　　机HTML＝机HTML＆「<tr>」
　　　　番号を0から12まで繰り返す
　　　　　　N=机[絵柄×13+番号]
　　　　　　絵＝（Nを番号カード変換）
　　　　　　机HTML＝机HTML＆「<td>{絵}</td>」
　　　　ここまで。
　　　　机HTML＝机HTML＆「</tr>」
　　ここまで。
　　机HTML=机HTML＆「</table>」
　　＃手札表示
　　手札HTML＝「」
　　手札HTML＝「<div>自分の手札:</div>」
　　Iを0から((参加者[0]の要素数)-1)まで繰り返す
　　　　N＝参加者[0][I]
　　　　絵＝Nを番号カード変換
　　　　もし（Nを机配置可能）ならば、絵＝「[{絵}]」。
　　　　手札HTML＝手札HTML＆「
<button onclick="window.カード提出({N})">{絵}</button>」
　　ここまで。
　　手札HTML＝手札HTML＆「<button onclick="window.提出パス()">パス</button>」
　　DOM親要素に「
<style>
  td {波カッコ} width:3em; text-align:center; {波カッコ閉}
</style>
<div>{机HTML}</div><br>
<div style="color:blue;">{情報}</div><br>
<div>{手札HTML}</div>
」をHTML設定。
ここまで。

#====================================
●(Nを)番号カード変換とは
　　もし、N<0ならば「＿」で戻る。
　　絵柄＝INT(N / 13)
　　絵＝カード絵柄[絵柄]&カード番号[N％13]。
　　もし、絵柄>1ならば、絵＝「<span style="color:red">{絵}</span>」
　　それは絵。
ここまで。

#====================================
●(Nを)机配置可能とは
　　絵柄＝INT(N/13)
　　番号＝N%13
　　もし番号＞6ならば
　　　　(机[N-1]≧0)で戻る。
　　違えば
　　　　(机[N+1]≧0)で戻る。
　　ここまで。
ここまで。

#====================================
# カードボタンを押した時のイベント
WINDOW["カード提出"] = 関数(N)
　　もし、誰ターン≠0ならば、戻る。
　　もし、Nを机配置可能でなければ
　　　　「そのカードは出せません」と言って戻る。
　　ここまで。
　　机[N]=N
　　I=参加者[0]からNを配列検索。
　　参加者[0]からIを配列削除。
　　もし、(参加者[0]の要素数)=0ならば
　　　　「あがり！！勝ちました！！」と言う。
　　　　ゲーム初期化して、戻る。
　　ここまで。
　　机表示。
　　1秒後には、
　　　　次ターン。
　　ここまで。
ここまで。

#====================================
# パスボタンを押した時のイベント
WINDOW["提出パス"] = 関数
　　パス回数[0]=パス回数[0]+1
　　もし、パス回数[0]＞3ならば
　　　　「パスは3回まで。あなたの負け。」と言う。
　　　　ゲーム初期化。
　　ここまで。
　　情報＝「パスしました：{パス回数[0]}/3」
　　机表示。
　　1秒後には
　　　　次ターン。
　　ここまで。
ここまで。

#====================================
# ターンを進める
#====================================
●次ターンとは
　　誰ターン＝（誰ターン＋1）％4
　　もし、誰ターン＝0ならば
　　　　情報＝「あなたの番です。」
　　　　机表示
　　　　0で戻る。
　　ここまで。
　　もし、パス回数[誰ターン]＞3ならば
　　　　情報＝「参加者{誰ターン}は負けてます」
　　　　机表示。
　　　　1秒後には
　　　　　　次ターン。
　　　　ここまで。
　　　　戻る。
　　ここまで。
　　＃コンピューター
　　C=参加者[誰ターン]の要素数
　　もし、C＝0ならば
　　　　情報＝「参加者{誰ターン}はあがっています。」
　　　　1秒後には
　　　　　　次ターン
　　　　ここまで。
　　　　戻る。
　　ここまで。
　　F=オフ
　　Iを0から(C-1)まで繰り返す
　　　　N=参加者[誰ターン][I]
　　　　もし、Nを机配置可能ならば
　　　　　　参加者[誰ターン]からIを配列削除。
　　　　　　机[N]=N
　　　　　　絵＝Nを番号カード変換。
　　　　　　情報＝「参加者{誰ターン}が{絵}を出しました」
　　　　　　机表示。
　　　　　　CC=参加者[誰ターン]の要素数
　　　　　　もし、CC=0ならば
　　　　　　　　情報＝「参加者{誰ターン}はあがりました」
　　　　　　　　机表示。
　　　　　　　　1秒後には
　　　　　　　　　　「参加者｛誰ターン｝の勝ち」と言う。
　　　　　　　　　　ゲーム初期化。
　　　　　　　　ここまで。
　　　　　　　　戻る。
　　　　　　ここまで。
　　　　　　F=オン。抜ける。
　　　　ここまで。
　　ここまで。
　　もし、F=オフならば
　　　　パス回数[誰ターン]＝パス回数[誰ターン]＋1
　　　　情報＝「参加者{誰ターン}はパスしました。{パス回数[誰ターン]}/3」
　　　　もし、パス回数[誰ターン]＞3ならば
　　　　　　Iで0から(C-1)まで繰り返す
　　　　　　　　N=参加者[誰ターン][I]
　　　　　　　　机[N]=N
　　　　　　ここまで。
　　　　　　情報＝「参加者{N}は負けました」
　　　　　　参加者[誰ターン] = []
　　　　ここまで。
　　　　机表示。
　　ここまで。
　　1秒後には
　　　　次ターン。
　　ここまで。
ここまで。