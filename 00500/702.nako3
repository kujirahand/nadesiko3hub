### [作品情報]
### タイトル=ch5/tokei_anarogu.nako3
### 作者=クジラ飛行机(user_id=1)
### ライセンス=MIT
### 説明=「なでしこ3本」5章のサンプル「tokei_anarogu.nako3」です。
### 対象バージョン=3.2.23
### URL=https://nadesi.com/top/go.php?46
### 種類=wnako
### タグ=なでしこ3本
### 更新日時=2021-07-09 17:14:02
###

### なでしこ3本 5章 のサンプル ###
# [file] ../src/ch5/tokei_anarogu.nako3
# [説明URL] https://nadesi.com/top/go.php?46

# 時計の描画サイズなどを指定
CX=150 # 中心点のX座標
CY=150 # 中心点のY座標
余白=5 # 左上マージン
「時計描画」を1秒毎。# --- (*1)

●時計描画とは
　　時計盤描画。
　　# 現在時刻を得る --- (*2)
　　変数[時,分,秒]＝（今を「:」で区切る）
　　# 角度計算 --- (*3)
　　時角度＝(時%12)÷12×360＋(分÷60)×30
　　分角度＝分÷60×360
　　秒角度＝秒÷60×360
　　# 針を描画 --- (*4)
　　黒色の15を(CX×0.5)と時角度で針描画。
　　青色の10を(CX×0.8)と分角度で針描画。
　　赤色の4を(CX×0.9)と秒角度で針描画。
ここまで。

●(色のサイズを長さと角度で)針描画とは # --- (*5)
　　RA=(角度-90)をラジアン変換
　　XX=CX+COS(RA)×長さ
　　YY=CY+SIN(RA)×長さ
　　色に線色設定。
　　サイズに線太さ設定。
　　始点＝[余白+CX, 余白+CY]
　　終点＝[余白+XX, 余白+YY]
　　始点から終点へ線描画。
 ここまで。

●時計盤描画とは
　　全描画クリア。
　　# 文字盤の背景を描画
　　2に線太設定。
　　黒色に線色設定。
　　白色に塗色設定。
　　[余白+CX,余白+CY]へCXの円描画。# 外側の円を描画
　　# 時間のメモリ描画 --- (*6)
　　時を0から11まで繰り返す
　　　　黒色の1をCXと(時÷12×360)で針描画。# 1時間ごとのメモリを描画亜
　　ここまで
　　[余白+CX,余白+CY]へ(CX×0.8)の円描画。# 内側の小さな円を描画
ここまで。