### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?2926
### タイトル=トランプを52枚順番にめくれるサンプル
### 作者=雪乃☆雫(user_id=0)
### ライセンス=CC0
### 説明=日本語プログラミング言語「なでしこ」 Advent Calendar 2024、二十日日目の記事用。
### 対象バージョン=3.6.37
### URL=
### 種類=wnako
### タグ=アドベントカレンダー2024,サンプル
### 更新日時=2024-12-19 14:12:39
###

＃ 52枚順番にめくれるサンプル
# 画面設定
変数 [画面幅,画面高さ]＝[300,300]。

# カード設定
変数 カード設定＝{
    "幅": 63, "高": 89,
    "絵柄": ["♣","♦","♥","♠"],
    "番号": ["A","2","3","4","5","6","7","8","9","10","J","Q","K"],
    "文字色": [緑色,金色,赤色,青色],
    "裏色": "#FFCCCC", "表色": 白色, "縁色": 白色
}
変数 親カード＝空配列。
変数 カード表＝空配列。
変数 カード裏＝空配列。
#--------------------------------------------------
# DOMスキンを作成
## スタイルを設定
DOM和スタイル追加。
変数 S＝DOM和スタイル。

「style」のDOM部品作成。
それに『.相対位置 {position: relative;}
.絶対位置 {position: absolute;}
.親 {transition: transform 0.6s;
    perspective: 300px;}
』&「.カード {波カッコ}
    {S.幅}: {カード設定.幅}px;
    {S.高さ}: {カード設定.高さ}px;
    {S.ボーダー}: solid 6px {カード設定.縁色};
    {S.角丸}: 14px;
    box-sizing: border-box;
    {S.背面}: {S.非表示};
    transition: transform 0.6s;
{波カッコ閉じ}
.表 {波カッコ}
    {S.余白}: 4px;
    {S."行揃え"}: {S.中央};
    {S.行高さ}: 1em;
    {S.文字サイズ}: 36px;
    {S.背景色}: {カード設定.表色};
{波カッコ閉じ}
.裏 {波カッコ}
    {S.背景色}: {カード設定.裏色};
{波カッコ閉じ}」をHTML設定。

## スキンを定義
DOMスキン辞書@「ゲーム画面」=関数(TYPE,OBJ)
　　OBJの「className」に「相対位置」をDOM属性設定。
ここまで。
DOMスキン辞書@「親カード」=関数(TYPE,OBJ)
　　OBJの「className」に「絶対位置 親」をDOM属性設定。
ここまで。
DOMスキン辞書@「カード表」=関数(TYPE,OBJ)
　　OBJの「className」に「絶対位置 カード 表」をDOM属性設定。
ここまで。
DOMスキン辞書@「カード裏」=関数(TYPE,OBJ)
　　OBJの「className」に「絶対位置 カード 裏」をDOM属性設定。
ここまで。
#--------------------------------------------------
# ゲーム画面の作成
「ゲーム画面」のDOMスキン設定。
ゲーム画面＝「div」のDOM部品作成。
ゲーム画面.幅＝画面幅&「px」。
ゲーム画面.高さ＝画面高さ&「px」。
ゲーム画面.背景色＝「green」

カード作成。
カードシャッフル。
●カード作成
    IDを0から51まで繰り返す：
        ゲーム画面にDOM親部品設定。
        # カードを作成
        「親カード」のDOMスキン設定。
        親カード[ID]は「div」のDOM部品作成。
        変数 カード＝親カード[ID]
        # カードをゲーム画面の中央に置く
        カード.左＝(画面幅/2-カード設定.幅-4)&「px」。
        カード.上＝(画面高さ-カード設定.高さ)/2&「px」。

        親カード[ID]にDOM親要素設定。
        # カードの表面を作成
        「カード表」のDOMスキン設定。
        カード表[ID]は「div」のDOM部品作成。
        変数 表カード＝カード表[ID]
        変数 カード絵柄＝カード設定.絵柄。
        変数 カード番号＝カード設定.番号。
        変数 カード文字色＝カード設定.文字色。
        変数 絵柄＝(ID/13)を整数変換。
        変数 番号＝ID%13。
        表カードに「{カード絵柄[絵柄]}<BR>{カード番号[番号]}」をテキスト設定。
        表カード.色＝カード文字色[絵柄]。
        表カードの「transform」に「rotateY(-180deg)」をDOMスタイル設定。  # 要素を、Y軸を中心に裏返しておく設定

        # カードの裏面を作成
        「カード裏」のDOMスキン設定。
        カード裏[ID]は「div」のDOM部品作成。
        変数 裏カード＝カード裏[ID]
        裏カードの「カードid」にIDをデータ属性設定。

        # イベント
        カードをクリックした時には
            ID＝対象の「カードid」をデータ属性取得。
            カード表[ID]の「transform」に「rotateY(0)」をDOMスタイル設定。
            カード裏[ID]の「transform」に「rotateY(180deg)」をDOMスタイル設定。

            x＝カード設定.幅＋8。
            親カード[ID]の「transform」に「translateX({x}px)」をDOMスタイル設定。
            順番＝親カード[ID]の「重なり」をDOMスタイル取得。
            0.2秒待つ
            親カード[ID]の「重なり」に51-順番をDOMスタイル設定。
        ここまで。
ここまで。

●カードシャッフル
    変数 カード＝0から51までの配列連番作成。
    カードを配列シャッフル。
    カードを反復。
        親カード[対象]の「重なり」に対象キーをDOMスタイル設定。
    ここまで。
    カードを戻す。
ここまで。
#--------------------------------------------------
●DOM和スタイル追加
	DOM和スタイル.角丸＝「border-radius」
	DOM和スタイル.背面＝「backface-visibility」
	DOM和スタイル.非表示＝「hidden」
    DOM和スタイル.行高＝「line-height」
ここまで。