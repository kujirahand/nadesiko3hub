### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?2669
### タイトル=三次元空間上の座標を四元数的に回転するツール
### 作者=NOU(user_id=0)
### ライセンス=MIT
### 説明=空間の座標をある軸を中心に回転させます。詳しくは四元数について解説しているサイト等をご覧になってください。
### 対象バージョン=3.4.22
### URL=
### 種類=wnako
### タグ=
### 更新日時=2023-11-03 23:46:20
###

「<h3>三次元空間上の座標を四元数的に回転するツール</h3>」のラベル作成。
「<h4>回転対象の座標を表す四元数</h4>」のラベル作成。
QQ=空の「
実部（0推奨）=0
iの係数=0
jの係数=0
kの係数=0
」をフォーム作成。
「<h4>回転軸のベクトル</h4>」のラベル作成。
VV=空の「
x成分=0
y成分=0
z成分=0
」をフォーム作成。
「<h4>まわりの角度</h4>」のラベル作成。
T=空の「
角度=0
単位=?(度数法（°）|弧度法（rad）)
」をフォーム作成。
「<h4>小数第何位を四捨五入するか</h4>」のラベル作成。
ROU=空の「
小数第=5
」をフォーム作成。
計算ボタン＝「計算する」のボタン作成。改行作成。
RES=「」のラベル作成。

計算ボタンをクリックした時には
    QQのフォーム入力一括取得してQ1に代入。
    VVのフォーム入力一括取得してV1に代入。
    Tのフォーム入力一括取得してT1に代入。
    ROUのフォーム入力一括取得してNに代入。
    Q=[Q1[「実部（0推奨）」],Q1[「iの係数」],Q1[「jの係数」],Q1[「kの係数」]]
    V=[V1[「x成分」],V1[「y成分」],V1[「z成分」]]
    theta=T1[「角度」]
    もしT1[「単位」]＝「度数法（°）」ならば
        thetaをラジアン変換して、thetaに代入。💧
    #RESにthetaをテキスト設定
    R=[COS((theta÷2)),V[0]×SIN(theta÷2),V[1]×SIN(theta÷2),V[2]×SIN(theta÷2)]
    
    RES1=（RにQを四元数乗算して、それに（Rの四元数共役）を四元数乗算して、それを（N[「小数第」]-1）で四元数四捨五入）
    RESに「<h1>計算結果</h1>回転後の座標を表す四元数は<br><b>{RES1の四元和表記}</b><br>です。これは、空間における座標<br><b>({RES1[1]},{RES1[2]},{RES1[3]})</b><br>を表します。」をテキスト設定。
ここまで。



●（PにQを）四元数加算とは
    A＝P＠０
    B＝P＠１
    C＝P＠２
    D＝P＠３
    E＝Q＠０
    F＝Q＠１
    G＝Q＠２
    H＝Q＠３
    実部＝A+E
    第一虚部＝B+F
    第二虚部＝C+G
    第三虚部＝D+H
    それ＝［実部、第一虚部、第二虚部、第三虚部］
ここまで。

●（PからQを）四元数減算とは
    A＝P＠０
    B＝P＠１
    C＝P＠２
    D＝P＠３
    E＝Q＠０
    F＝Q＠１
    G＝Q＠２
    H＝Q＠３
    実部＝A-E
    第一虚部＝B-F
    第二虚部＝C-G
    第三虚部＝D-H
    それ＝［実部、第一虚部、第二虚部、第三虚部］
ここまで。

●（PにQを）四元数乗算とは
    A＝P＠０
    B＝P＠１
    C＝P＠２
    D＝P＠３
    E＝Q＠０
    F＝Q＠１
    G＝Q＠２
    H＝Q＠３
    実部＝A*E-B*F-C*G-D*H
    第一虚部＝A*F+B*E+C*H-D*G
    第二虚部＝A*G-B*H+C*E+D*F
    第三虚部＝A*H+B*G-C*F+D*E
    それ＝［実部、第一虚部、第二虚部、第三虚部］
ここまで。
    
●（Qの）四元数共役とは
    A＝Q＠０
    B＝Q＠１
    C＝Q＠２
    D＝Q＠３
    それ＝［A、-B、-C、-D］
ここまで。

●（Qの）四元数絶対値とは
    Qに（Qの四元数共役）を四元数乗算して、Qに代入。
    AにQ＠０を代入。
    Aの平方根をそれに代入。
ここまで。

●（Qの）四元数平方絶対値とは
    Qに（Qの四元数共役）を四元数乗算して、Qに代入。
    AにQ＠０を代入。
    Aをそれに代入。
ここまで。

●（Qの）四元数逆数とは
    Qの四元数共役をQ1に代入。
    Qの四元数平方絶対値をQ2に代入。
　　A＝Q1＠０
    B＝Q1＠１
    C＝Q1＠２
    D＝Q1＠３
    それに［A/Q2、B/Q2、C/Q2、D/Q2］を代入。
ここまで。

●（PをQで）四元数除算とは
    Pに（Qの四元数逆数）を四元数乗算して、それに代入。
ここまで。

●（Nの）文字列符号とは
    Nの符号を判定に代入
    もし判定＝-1ならば
        それに「-」を代入。
    違えばもし判定＝0ならば
        それに「」を代入。
    違えばもし判定＝1ならば
        それに「+」を代入。
    ここまで。
ここまで。

●（QをNで）四元数四捨五入とは
    Q［0］をNで小数点四捨五入して、Q1に代入。
    Q［1］をNで小数点四捨五入して、Q2に代入。
    Q［2］をNで小数点四捨五入して、Q3に代入。
    Q［3］をNで小数点四捨五入して、Q4に代入。
    それ＝［Q1,Q2,Q3,Q4］
ここまで。

●（Qの）四元和表記とは
    A＝Q＠０
    B＝Q＠１
    C＝Q＠２
    D＝Q＠３
    もしA＝０なら　A＝「」
    もしB＝０なら　B＝「」
    もしC＝０なら　C＝「」
    もしD＝０なら　D＝「」
    返値に（Aを文字列変換）＆（Bの文字列符号）＆（（Bの絶対値）を文字列変換）＆「i」＆（Cの文字列符号）＆（（Cの絶対値）を文字列変換）＆「j」＆（Dの文字列符号）＆（（Dの絶対値）を文字列変換）＆「k」を代入
    返値の「+1i」を「+i」に置換して返値に代入。
    返値の「+1j」を「+j」に置換して返値に代入。
    返値の「+1k」を「+k」に置換して返値に代入。
    返値の「-1i」を「-i」に置換して返値に代入。
    返値の「-1j」を「-j」に置換して返値に代入。
    返値の「-1k」を「-k」に置換して返値に代入。
    返値の「-0i」を「」に置換して返値に代入。
    返値の「-0j」を「」に置換して返値に代入。
    返値の「-0k」を「」に置換して返値に代入。
    もし（返値で「+」が何文字目か）＝1ならば　返値の1から1だけ文字削除して返値に代入。
    もし返値＝「」ならば　返値に「0」を代入。
　　それに返値を代入。
ここまで。