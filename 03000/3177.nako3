### [作品情報]
### 掲載URL=https://n3s.nadesi.com/id.php?3177
### タイトル=キャンバス作成[100,100]
### 作者=e-fujii(user_id=0)
### ライセンス=MIT
### 説明=普通のアナログ時計です。
### 対象バージョン=3.7.14
### URL=e-fujii@jcom.home.ne.jp
### 種類=wnako
### タグ=
### 更新日時=2026-01-31 15:17:21
###

#キャンバス作成
[100,100]のキャンバス作成。

#時計更新
「時計更新」を0.1秒毎。

●時計更新とは

#時計枠描画
全描画クリア
1に線太設定。
黒色に線色設定。
緑色に塗色設定。
[0,0,100,100]へ四角描画。
RGB(200,255,255)に塗色設定。
[50,50]に50の円描画。

#文字描画
赤色に塗色設定。
「16px sans-serif」に描画フォント設定。
[42,21]に「12」の文字描画。
[81,54]に「3」の文字描画。
[46,90]に「6」の文字描画。
[10,54]に「9」の文字描画。

#時目盛り描画
p=0
12回
　ｑ＝DEG2RAD(p)
　X=SIN(q)*45
　Y=COS(q)*45
　黒色に塗色設定。
    [50+X,50+Y]に2の円描画。
 　p＝p+30
ここまで

#時刻データ取得
時時は今で2文字左部分。
分分は今で4から2文字抜き出す。
秒秒は今で2文字右部分。

もし、時時<12ならば
　[38,40]に「AM」を文字描画。
違えば
　[38,40]に「PM」を文字描画。
ここまで
「10px sans-serif」に描画フォント設定。
[25,65]に「NADESIKO」を文字描画。

#時針の位置計算と表示
時角1は時時+分分/60
時角2はDEG2RAD(時角1*30)
X=SIN(時角2)*30
Y=COS(時角2)*30
RGB(0,150,0)に線色設定
6に線太設定
[50,50]から[50+X,50-Y]へ線描画

#分針の位置計算と表示
分角1は分分+秒秒/60
分角2はDEG2RAD(分角1*6)
X=SIN(分角2)*35
Y=COS(分角2)*35
青色に線色設定
4に線太設定
[50,50]から[50+X,50-Y]へ線描画

#秒針の位置計算と表示
秒角はDEG2RAD(秒秒*6)
X=SIN(秒角)*45
Y=COS(秒角)*45
赤色に線色設定
1に線太設定
[50,50]から[50+X,50-Y]へ線描画
赤色に塗色設定。
[50,50]に5の円描画。

ここまで